import com.bmuschko.gradle.docker.tasks.container.*

plugins {
  id "java"
  id "scala"
  id "application"
  id "cz.alenkacz.gradle.scalafmt" version "1.16.2"
  id 'com.bmuschko.docker-remote-api' version '9.0.1'
}

version = '1.0.2'

mainClassName = "io.sdkman.DbMigration"

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {
  implementation 'org.scala-lang:scala-library:2.12.12'
  implementation 'org.javassist:javassist:3.18.2-GA'
  implementation 'com.github.mongobee:mongobee:0.13'
  implementation 'com.typesafe:config:1.3.1'
  implementation 'com.github.sdkman:sdkman-url-validator:0.2.5'
  runtimeOnly 'org.slf4j:slf4j-simple:1.7.25'
}

compileScala.dependsOn("checkScalafmt")

task createMongoContainer(type: DockerCreateContainer) {
  targetImageId 'mongo:3.2'
  containerName = 'mongo'
  hostConfig.portBindings = ['27017:27017']
  hostConfig.autoRemove = true
}

task startMongoContainer(type: DockerStartContainer) {
  dependsOn createMongoContainer
  targetContainerId createMongoContainer.containerId
}

task stopMongoContainer(type: DockerStopContainer) {
  targetContainerId createMongoContainer.containerId
}

run.dependsOn(startMongoContainer)

run.finalizedBy(stopMongoContainer)